open module content;
extends "def.pkl"
import "licenses.pkl" as licenses

hidden const isDefault = read?("env:default") == "true"

const function parseVersion(version: String) = 
    let(split = version.split("."))
    1_000_000 * split[0].toInt() +
    10_000    * split[1].toInt() +
    1         * split[2].takeWhile((x) -> x.matches(Regex(#"^\d*$"#))).toInt()

const function isPlatform(platforms: List<Platform>) = platforms.contains(read?("env:platform"))
const function versionGEQ(version: String) = 
    let(envVersion = read?("env:version"))
    if (envVersion == null)
        isDefault
    else
        parseVersion(envVersion) >= parseVersion(version)
const function versionL(version: String) = 
    let(envVersion = read?("env:version"))
    if (envVersion == null)
        true
    else
        parseVersion(envVersion) < parseVersion(version)

pages = new Listing<Content> {
    new Content {
        id = "more"
        title = new Translated {
            default = "More"
            zh = "æ›´å¤š"
        }.pick()
        content = ""
        contentLinks = new {
            new ContentLink {
                title = new Translated {
                    default = "Service Updates"
                    zh = "æœåŠ¡è­¦æŠ¥"
                }.pick()
                contentId = "service-alerts"
                order = 1000
            }
            new ContentLink {
                title = new Translated {
                    default = "About this App"
                    zh = "å…³äºè¿™ä¸ªåº”ç”¨ç¨‹åº"
                }.pick()
                contentId = "about"
                order = 2000
            }
        }
        nativeLinks = new {
            new NativeLink {
                title = new Translated {
                    default = "Settings"
                    zh = "è®¾ç½®"
                }.pick()
                nativeReference = "preferences"
                order = 500
            }
        }
    }
    new Content {
        id = "about"
        title = new Translated {
            default = "About"
            zh = "å…³äº"
        }.pick()
        content = """
        Sinatra is a companion app for the Canberra MyWay+ network made by locals for locals. Our goal is to make it easier to find route and stop information, offering a fresh, streamlined experience alongside the official app.

        Made with ğŸ©· in Canberra, Australia.\(if (isPlatform(List("android"))) " You can find and contribute to our codebase on [Github](https://github.com/emilymclean/sinatra)." else "")
        """
        externalLinks = new {
            new ExternalLink {
                title = new Translated {
                    default = "Terms and Conditions"
                    zh = "æ¡æ¬¾å’Œæ¡ä»¶"
                }.pick()
                url = "https://sinatra-transport.com/terms"
                order = 3000
            }
            new ExternalLink {
                title = new Translated {
                    default = "Privacy Policy"
                    zh = "éšç§æ”¿ç­–"
                }.pick()
                url = "https://sinatra-transport.com/privacy"
                order = 4000
            }
        }
        contentLinks = new {
            ...(if (versionL("0.11.0"))
                new Listing {
                    new ContentLink {
                        title = new Translated {
                            default = "Service Updates"
                            zh = "æœåŠ¡è­¦æŠ¥"
                        }.pick()
                        contentId = "service-alerts"
                        order = 500
                    }
                }
            else
                new Listing {})
            new ContentLink {
                title = new Translated {
                    default = "Third Party Notices"
                    zh = "ç¬¬ä¸‰æ–¹è®¸å¯ä¿¡æ¯"
                }.pick()
                contentId = "attribution"
                order = 2000
            }
        }
    }
    new Content {
        id = "attribution"
        title = new Translated {
            default = "Third Party Notices"
            zh = "ç¬¬ä¸‰æ–¹è®¸å¯ä¿¡æ¯"
        }.pick()
        content = """
        The following sets forth attribution notices for third party software that may be contained in portions of this app.

        \(licenses.appLicense.description())

        Sinatra is not affiliated with Transport Canberra, NEC, or MyWay+.
        """
    }
    new Content {
        id = "service-alerts"
        title = new Translated {
            default = "Service Updates"
            zh = "æœåŠ¡è­¦æŠ¥"
        }.pick()
        content = "Update to the latest version of Sinatra to view service alerts and updates, or visit Transport Canberra's website at [transport.act.gov.au](https://www.transport.act.gov.au/news/service-alerts-and-updates)."
    }
}

banners = new Mapping<String, Banner> {}