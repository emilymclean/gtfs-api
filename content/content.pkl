open module content;
extends "def.pkl"
import "licenses.pkl" as licenses

const function parseVersion(version: String) = 
    let(split = version.split("."))
    1_000_000 * split[0].toInt() +
    10_000    * split[1].toInt() +
    1         * split[2].takeWhile((x) -> x.matches(Regex(#"^\d*$"#))).toInt()

const function isPlatform(platforms: List<Platform>) = platforms.contains(read?("env:platform"))
const function versionGEQ(version: String) = 
    let(envVersion = read?("env:version"))
    if (envVersion == null)
        false
    else
        parseVersion(envVersion) >= parseVersion(version)

pages = new Listing<Content> {
    new Content {
        id = "about"
        title = "About"
        content = """
        Sinatra is a companion app for the Canberra MyWay+ network made by locals for locals. Our goal is to make it easier to find route and stop information, offering a fresh, streamlined experience alongside the official app.

        Made with ðŸ©· in Canberra, Australia.\(if (isPlatform(List("android"))) " You can find and contribute to our codebase on [Github](https://github.com/emilymclean/sinatra)." else "")

        Sinatra is not affiliated with Transport Canberra, NEC, or MyWay+.
        """
        externalLinks = new {
            ...(if (isPlatform(List("android"))) 
                new Listing {
                    new ExternalLink {
                        title = "Donate"
                        url = "https://ko-fi.com/emilymclean"
                        order = 1000
                    }
                }
            else
                new Listing {})
            new ExternalLink {
                title = "Terms and Conditions"
                url = "https://sinatra-transport.com/terms"
                order = 3000
            }
            new ExternalLink {
                title = "Privacy Policy"
                url = "https://sinatra-transport.com/privacy"
                order = 4000
            }
        }
        contentLinks = new {
            new ContentLink {
                title = "Service Updates"
                contentId = "service-alerts"
                order = 500
            }
            new ContentLink {
                title = "Third Party Notices"
                contentId = "attribution"
                order = 2000
            }
        }
        nativeLinks = new {
            
        }
    }
    new Content {
        id = "attribution"
        title = "Third Party Notices"
        content = """
        The following sets forth attribution notices for third party software that may be contained in portions of this app.

        \(licenses.appLicense.description())
        """
    }
    new Content {
        id = "service-alerts"
        title = "Service Alerts"
        content = "Update to the latest version of Sinatra to view service alerts and updates, or visit Transport Canberra's website at [transport.act.gov.au](https://www.transport.act.gov.au/news/service-alerts-and-updates)."
    }
}

banners = new Mapping<String, Banner> {}