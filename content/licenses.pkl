typealias LicenseCode = "A2.0" | "MIT" | "ODbL" | "MISC" | "CCBY4.0"

class License {
    name: String
    url: String? = null
}

local const licenses = new Mapping<LicenseCode, License> {
    ["A2.0"] = new License {
        name = "Apache License 2.0"
        url = "https://www.apache.org/licenses/LICENSE-2.0"
    }
    ["MIT"] = new License {
        name = "MIT License"
        url = "https://opensource.org/license/mit"
    }
    ["ODbL"] = new License {
        name = "Open Data Commons Open Database License 1.0"
        url = "https://opendatacommons.org/licenses/odbl/"
    }
    ["MISC"] = new License {
        name = "Miscellaneous"
    }
    ["CCBY4.0"] = new License {
        name = "Creative Commons Attribution 4.0 International"
        url = "https://creativecommons.org/licenses/by/4.0/"
    }
}

class LicensedContent {
    name: String
    attribution: String? = null
    year: String? = null
    note: String? = null
    licenseCode: LicenseCode
}

local class GroupedLicensedContent {
    code: LicenseCode
    content: Listing<LicensedContent>
}

class LicenseCollection {
    content: Listing<LicensedContent>

    local function groupByLicense(): Listing<GroupedLicensedContent> =
        let(usedLicenses = content.toList().map((e) -> (e.licenseCode)).distinct)
        let(_content = content)
        usedLicenses.map(
            (l) -> new GroupedLicensedContent {
                code = l
                content = _content.toList().filter((e) -> e.licenseCode == l).sortBy((e) -> e.name).toListing()
            }
        ).sortBy((e) -> e.code).toListing()

    local function licensedContentDescription(_content: Listing<LicensedContent>): String =
        _content.toList().map((e) -> 
            let (
                copyright = if (e.attribution != null && e.year != null)
                    "Copyright (c) \(e.year) \(e.attribution)"
                else if (e.attribution != null)
                    "Copyright (c) \(e.attribution)"
                else if (e.year != null)
                    "Copyright (c) \(e.year)"
                else null
            )
            new Listing {
                "* "
                if (e.note == null) "**\(e.name)**" else null
                if (e.note != null) "\(e.note)" else null
                if (e.note == null && copyright != null) ", " else null
                if (copyright != null) "\(copyright)" else null
            }.toList().filterNonNull().join("")
        ).join("\n").trim()

    function description(): String = 
        let(grouped = groupByLicense())
        grouped.toList().map((e) -> 
            let(license = licenses[e.code])
            let(contentDescription = licensedContentDescription(e.content))
            if (license.url != null)
                """
                #### \(license.name)
                The following components are licensed under the [\(license.name)](\(license.url)):

                \(contentDescription)
                """
            else
                """
                #### \(license.name)

                \(contentDescription)
                """
        ).join("\n\n")

}

hidden appLicense = new LicenseCollection {
    content = new {
        new {
            name = "Transport Canberra Data"
            attribution = "Transport Canberra & the ACT Government"
            year = "2025"
            licenseCode = "CCBY4.0"
            note = "This application uses transport network data provided by Transport Canberra under the Creative Commons Attribution 4.0 International (CC BY 4.0) License. The data includes bus routes, timetables, stop locations, service alerts, and real-time information."
        }
        new {
            name = "Koin"
            attribution = "Arnaud GIULIANI, Laurent BARESSE"
            year = "2017"
            licenseCode = "A2.0"
        }
        new {
            name = "Ktorfit"
            attribution = "Jens Klingenberg"
            year = "2022"
            licenseCode = "A2.0"
        }
        new {
            name = "Ktor"
            attribution = "JetBrains s.r.o"
            year = "2000-2023"
            licenseCode = "A2.0"
        }
        new {
            name = "Napier"
            attribution = "AAkira"
            year = "2019"
            licenseCode = "A2.0"
        }
        new {
            name = "Kotlin Datetime"
            attribution = "JetBrains s.r.o"
            year = "2019-2020"
            licenseCode = "A2.0"
        }
        new {
            name = "Units"
            attribution = "Emily McLean"
            year = "2024"
            licenseCode = "MIT"
        }
        new {
            name = "Serializable"
            attribution = "Emily McLean"
            year = "2024"
            licenseCode = "MIT"
        }
        new {
            name = "RequestState"
            attribution = "Emily McLean"
            year = "2024"
            licenseCode = "MIT"
        }
        new {
            name = "ErrorWidget"
            attribution = "Emily McLean"
            year = "2024"
            licenseCode = "MIT"
        }
        new {
            name = "pbandk"
            attribution = "Chad Retz"
            year = "2018"
            licenseCode = "MIT"
        }
        new {
            name = "Coil"
            attribution = "Coil Contributors"
            year = "2024"
            licenseCode = "A2.0"
        }
        new {
            name = "AOSP"
            attribution = "The Android Open Source Project"
            year = "2020"
            licenseCode = "A2.0"
        }
        new {
            name = "Moko Permissions"
            attribution = "IceRock MAG Inc"
            year = "2025"
            licenseCode = "A2.0"
        }
        new {
            name = "Firebase"
            attribution = "Google LLC"
            year = "2019"
            licenseCode = "A2.0"
        }
        new {
            name = "Voyager"
            attribution = "Adriel Caf√©"
            year = "2021"
            licenseCode = "MIT"
        }
        new {
            name = "Autosize Text"
            attribution = "Reda El Madini"
            licenseCode = "MIT"
        }
        new {
            name = "Address Information/Nominatim"
            attribution = "OpenStreetMap contributors, [http://osm.org/copyright](http://osm.org/copyright)"
            licenseCode = "ODbL"
        }
        new {
            name = "Fibonacci Heap"
            attribution = "[Keith Schwarz](https://www.keithschwarz.com/interesting/)"
            year = "2010"
            licenseCode = "MISC"
        }
        new {
            name = "ECBExtensions"
            attribution = "goto10"
            year = "2010"
            licenseCode = "MIT"
        }
        new {
            name = "Kotlin Natural Sort"
            attribution = "MajorTanya"
            licenseCode = "A2.0"
        }
    }
}